import org.flywaydb.gradle.task.FlywayMigrateTask

buildscript {
    ext.kotlin_version = '1.1.4-3'
}

plugins {
    id 'java'
    id 'org.springframework.boot' version '1.5.4.RELEASE'
    id 'org.jetbrains.kotlin.jvm' version '1.1.4-3'
    id 'org.jetbrains.kotlin.plugin.spring' version '1.1.4-3'
    id 'org.jetbrains.kotlin.plugin.noarg' version '1.1.4-3'
    id 'org.jetbrains.kotlin.plugin.jpa' version '1.1.4-3'
    id 'org.flywaydb.flyway' version '4.2.0'
}

repositories {
    mavenCentral()
}

compileKotlin {
    kotlinOptions {
        jvmTarget = '1.8'
    }
}
compileTestKotlin {
    kotlinOptions {
        jvmTarget = '1.8'
    }
}

dependencies {
    compile "org.jetbrains.kotlin:kotlin-stdlib-jre8:$kotlin_version"
    compile 'org.springframework.boot:spring-boot-starter-web'
    compile 'org.springframework.boot:spring-boot-starter-actuator'
    compile 'org.springframework.boot:spring-boot-starter-data-jpa'
    compile 'com.fasterxml.jackson.module:jackson-module-kotlin:2.9.0'
    compile 'mysql:mysql-connector-java:6.0.6'
    testCompile 'com.nhaarman:mockito-kotlin:1.5.0'
    testCompile 'org.springframework.boot:spring-boot-starter-test'
}

def developmentDbUrl = "jdbc:mysql://localhost:3306/tracker_dev?user=tracker&useSSL=false&useTimezone=true&serverTimezone=UTC&useLegacyDatetimeCode=false"
bootRun.environment([
        "WELCOME_MESSAGE": "hello",
        "SPRING_DATASOURCE_URL": developmentDbUrl,
        "MANAGEMENT_SECURITY_ENABLED": false,
])

def testDbUrl = "jdbc:mysql://localhost:3306/tracker_test?user=tracker&useSSL=false&useTimezone=true&serverTimezone=UTC&useLegacyDatetimeCode=false"
test.environment([
        "WELCOME_MESSAGE": "Hello from test",
        "SPRING_DATASOURCE_URL": testDbUrl,
        "MANAGEMENT_SECURITY_ENABLED": false,
])

flyway {
    url = developmentDbUrl
    user = "tracker"
    password = ""
    locations = ["filesystem:databases/tracker/migrations"]
}

task testMigrate(type: FlywayMigrateTask) {
    url = testDbUrl
}
